cmake_minimum_required(VERSION 3.19)

set(jpge_known_comps static shared)
set(jpge_comp_static NO)
set(jpge_comp_shared NO)
foreach (jpge_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (jpge_comp IN_LIST jpge_known_comps)
        set(jpge_comp_${jpge_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "jpge does not recognize component `${jpge_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (jpge_comp_static AND jpge_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "jpge `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(jpge_static_targets "${CMAKE_CURRENT_LIST_DIR}/jpge-static-targets.cmake")
set(jpge_shared_targets "${CMAKE_CURRENT_LIST_DIR}/jpge-shared-targets.cmake")

macro(jpge_load_targets type)
    if (NOT EXISTS "${jpge_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "jpge `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${jpge_${type}_targets}")
endmacro()

if (jpge_comp_static)
    jpge_load_targets(static)
elseif (jpge_comp_shared)
    jpge_load_targets(shared)
elseif (DEFINED jpge_SHARED_LIBS AND jpge_SHARED_LIBS)
    jpge_load_targets(shared)
elseif (DEFINED jpge_SHARED_LIBS AND NOT jpge_SHARED_LIBS)
    jpge_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${jpge_shared_targets}")
        jpge_load_targets(shared)
    else ()
        jpge_load_targets(static)
    endif ()
else ()
    if (EXISTS "${jpge_static_targets}")
        jpge_load_targets(static)
    else ()
        jpge_load_targets(shared)
    endif ()
endif ()
